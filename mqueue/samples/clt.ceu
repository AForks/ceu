input  int         Radio_startDone;
input  _message_t* Radio_receive;
output _message_t* Radio_send;

loop do
    int err = _Radio_start();
    if err == _SUCCESS then
        err = await Radio_startDone;
        if err == _SUCCESS then
            break;
        end
    end
    await 1s;
end
_DBG("Radio started\n");

event void received;

loop do
    par/or do
        await 2s;

    with
        _Leds_set(0);

        _message_t sendMsg;
        _Radio_setSource(&sendMsg, _TOS_NODE_ID);
        _Radio_setDestination(&sendMsg, 10);
        emit Radio_send(&sendMsg);

        int v;
        loop do
            _message_t* recvMsg = await Radio_receive;
            if _Radio_getDestination(recvMsg) == _TOS_NODE_ID then
                int* pv = _Radio_getPayload(recvMsg, sizeof<int>);
                v = *pv;
                break;
            end
        end
        emit received;

        _DBG("Received = %d\n", v);
        _Leds_set(v);
        await forever;

    with
        par/or do
            await 1s;
            _DBG("Timeout\n");
            loop do
                _Leds_led0Toggle();
                await 100ms;
            end
        with
            await received;
        end
        await forever;
    end
end
